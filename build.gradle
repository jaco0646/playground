buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion")
    }
}

plugins {
    id 'groovy'
    id 'org.springframework.boot' version "$springBootVersion"
//    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'io.freefair.aspectj.post-compile-weaving' version '5.3.0'
    id 'project-report'  // htmlDependencyReport
}

// https://docs.freefair.io/gradle-plugins/current/reference/#_aspectj_plugins
aspectj {
    version = "1.9.6"
}

group 'com.playground'
version '1.0-SNAPSHOT'

sourceCompatibility = 14
targetCompatibility = 14

tasks.withType(JavaCompile) {
    options.compilerArgs += '--enable-preview'
}
tasks.withType(Test) {
    jvmArgs += '--enable-preview'
}
bootRun {
    jvmArgs = ['--enable-preview']
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.codehaus.groovy:groovy-all:3.0.6'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.11.2'
    implementation 'org.apache.commons:commons-collections4:4.4'
    implementation 'org.reflections:reflections:0.9.12'

    implementation "org.springframework:spring-core:$springVersion"
    implementation "org.springframework:spring-webmvc:$springVersion"
    implementation "org.springframework.boot:spring-boot:$springBootVersion"
    implementation "org.springframework.boot:spring-boot-autoconfigure:$springBootVersion"
    implementation "org.springframework.boot:spring-boot-starter-tomcat:$springBootVersion"
    implementation "org.springframework.boot:spring-boot-devtools:$springBootVersion"

    compileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'

    testImplementation 'org.spockframework:spock-core:2.0-M4-groovy-3.0'
}

test {
    useJUnitPlatform()
    afterSuite { desc, result ->
        if (!desc.parent) {
            if (result.testCount == 0) {
                throw new IllegalStateException("No tests were found. Failing the build.")
            }
        }
    }
}