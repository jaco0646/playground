import org.springframework.boot.gradle.tasks.run.BootRun
import playground.plugin.bytebuddy.ImmutableEntityPlugin

import java.time.ZonedDateTime
import java.time.format.DateTimeFormatter

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$springPluginVersion")
    }
}

plugins {
    id 'idea'  // Automatically downloads source jars but not Javadoc.
    id 'groovy'
    id 'scala'
    id 'org.springframework.boot' version "$springPluginVersion"
//    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'project-report'  // htmlDependencyReport
    id 'com.gorylenko.gradle-git-properties' version '2.5.7'
    id 'com.google.cloud.tools.jib' version '3.5.3'  // jibDockerBuild
    id 'net.bytebuddy.byte-buddy-gradle-plugin' version '1.18.5'
}

idea {
    module {
        downloadJavadoc = true
    }
}

group = 'com.playground'
version = '1.0-SNAPSHOT'

java {
    sourceCompatibility = 17
    targetCompatibility = 17
}

// https://docs.gradle.org/9.3.1/userguide/scala_plugin.html#sec:changing_scala_project_layout
sourceSets.main.scala.srcDirs += ['scala2/src/main/scala/com/external']

// https://docs.gradle.org/9.3.1/userguide/building_java_projects.html#sec:building_jvm_lang
tasks.named('compileScala') {
    // Scala only needs the declared dependencies
    // (and not the output of compileJava)
    classpath = sourceSets.main.compileClasspath
}
tasks.named('compileJava') {
    // Java depends on the result of Scala compilation
    // (which automatically makes it depend on compileScala)
    classpath += files(sourceSets.main.scala.classesDirectory)
}

byteBuddy {
    transformation {
        plugin = ImmutableEntityPlugin.class
    }
}

jib {
    from.image = 'adoptopenjdk:16-jre'
    to.image = "$group/$version".toLowerCase()
    container.creationTime = 'USE_CURRENT_TIMESTAMP'
    container.mainClass = 'spring.Application'
//    container.jvmFlags = ['--enable-preview']
    container.ports = ['8080']
}

springBoot {
    mainClass.set(jib.container.mainClass)
}

// For actuator/info
gitProperties {
    dateFormat = 'yyyy-MM-dd h:mm a z'
    keys = ['git.branch', 'git.build.version', 'git.commit.id', 'git.commit.time']
    customProperty 'git.commit.count', { it.repository.jgit.log().call().size() }  // https://github.com/n0mer/gradle-git-properties/blob/master/src/main/groovy/com/gorylenko/properties/CacheSupport.groovy#L30
    customProperty 'git.build.artifact', { bootJar.archiveFileName.get() }
    customProperty 'git.build.java-version', { System.getProperty('java.version') }
    customProperty 'git.build.spring-boot-version', { springBootVersion }
    customProperty 'git.build.time', { DateTimeFormatter.ofPattern(dateFormat).format(ZonedDateTime.now()) }
}

//tasks.withType(JavaCompile) {
//    options.compilerArgs += '--enable-preview'
//}
//tasks.withType(Test) {
//    jvmArgs += '--enable-preview'
//}
//tasks.withType(BootRun) {
//    jvmArgs = ['--enable-preview']
//}

tasks.register('bootRunLocal', BootRun) {
    it.group = 'Application'
    it.description = /Equivalent to gradle bootRun --args='--spring.profiles.active=local'/
    it.dependsOn = [bootJar]
    it.mainClass = bootJar.mainClass
    it.classpath = sourceSets.main.runtimeClasspath
    it.args = ['--spring.profiles.active=local']
}

repositories {
    mavenCentral()
}

dependencies {
    // Scala
    implementation("org.scala-lang:scala-library:2.12.21")
    // Groovy
    implementation 'org.apache.groovy:groovy-all:4.0.13'
    // XML
    implementation 'com.fasterxml.jackson.module:jackson-module-mrbean:2.17.0'
    implementation 'javax.xml.bind:jaxb-api:2.3.1'
    // Commons
    implementation 'org.apache.commons:commons-collections4:4.4'
    implementation 'commons-io:commons-io:2.11.0'
    // Misc
    implementation 'org.seleniumhq.selenium:selenium-java:4.5.0'
    implementation 'org.seleniumhq.selenium:selenium-http-jdk-client:4.5.0'
    implementation 'org.reflections:reflections:0.10.2'
    // Spring
    implementation "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa:$springBootVersion"
    implementation "org.springframework.boot:spring-boot-starter-actuator:$springBootVersion"
    implementation "org.springframework.boot:spring-boot-starter-validation:$springBootVersion"
    implementation "org.springframework.boot:spring-boot-devtools:$springBootVersion"
    implementation "org.springframework.data:spring-data-envers:$springBootVersion"
    implementation "org.springframework.cloud:spring-cloud-starter-openfeign:$springCloudVersion"
    runtimeOnly "io.awspring.cloud:spring-cloud-starter-aws-secrets-manager-config:2.4.4"
    // Swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.2.0'
    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'
    // Database
    runtimeOnly 'org.postgresql:postgresql:42.2.20'
    runtimeOnly 'com.h2database:h2:2.2.222'
    // Test
    testImplementation "org.spockframework:spock-core:$spockVersion"
    testImplementation "org.spockframework:spock-spring:$spockVersion"
    testImplementation "org.springframework.boot:spring-boot-starter-test:$springBootVersion"
    testImplementation 'com.github.tomakehurst:wiremock-jre8:2.29.1'
    testImplementation 'io.github.http-builder-ng:http-builder-ng-core:1.0.4'
}

test {
    useJUnitPlatform()
    afterSuite { desc, result ->
        if (!desc.parent) {
            if (result.testCount == 0) {
                throw new IllegalStateException("No tests were found. Failing the build.")
            }
        }
    }
}